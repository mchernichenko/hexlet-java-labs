plugins {
  id 'org.springframework.boot' version '2.5.5'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
  id 'com.adarshr.test-logger' version '3.0.0'
  id 'org.liquibase.gradle' version '2.0.4'
}

group = 'exercise'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  implementation 'org.liquibase:liquibase-core'
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  runtimeOnly 'com.h2database:h2:1.4.200'

  // Подключаем зависимости, необходимые для работы плагина
  liquibaseRuntime 'org.liquibase:liquibase-core:3.8.1'
  liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
  liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.6'
  liquibaseRuntime sourceSets.main.output
}

test {
  useJUnitPlatform()
}

testlogger {
  showStandardStreams = true
}

// Настраиваем таску diffChangeLog таким образом,
// чтобы миграция генерировалась при каждом изменении в модели
diffChangeLog {
  dependsOn compileJava
}

// Настраиваем Liquibase
// Как конфигурировать плагин см. https://github.com/liquibase/liquibase-gradle-plugin
liquibase {  // блок конфигурации liquibase
  activities { // список активностей (методов), в данном случае только метод main
    main {     // команды в методах - это тоже самое, если их запустить в командной строке или указать в .properties (см. ДЗ migrations)
      // Указываем путь, по которому будет сгенерирован файл миграции
      changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
      // Указывем источник, с которым будут сравниваться изменения
      // Это база данных, изначально она пустая
      url 'jdbc:h2:./hexlet' // jdbc:postgresql://172.25.120.42:5432/hexlet_db
      // Имя пользователя и пароль для подключения к базе
      username 'sa' // postgres
      password ''
      // Указываем, откуда мы будем брать изменения
      // Это модели
      // Скрипт, который создаст liquibase после сравнения будет представлять из себя разницу между моделью по ссылке referenceUrl и базой по ссылке url.
      // Указываем пакет, в котором расположены модели, где 'hibernate:spring' указание на то, что сравнение с моделями, хотя м.б. указана другая БД
      referenceUrl 'hibernate:spring:exercise.model' +
                    // Указываем диалект
                    // dialect=org.hibernate.dialect.Oracle12cDialect - An SQL dialect for Oracle 12c.
                    // dialect=org.hibernate.dialect.PostgreSQL95Dialect -An SQL dialect for Postgres 9.5 and later.
                   '?dialect=org.hibernate.dialect.H2Dialect' +
                   // Указываем правила именования таблиц и столбцов,
                   // чтобы они соответствовали правилам Spring
                   '&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
    }
  }
}
